;***********************************************************
;
;               BLITZKRIEG INTEGER MULTIPLY
;
;               VERSION         2B
;
;               LARRY BRYANT & BILL SEILER
;               AUG 19,1974
;       MULT TWO SINGLE WORD INTEGERS TO PRODUCE
;       A DOUBLE WORD RESULT
;       R0=MULTIPLIER (UNCHANGED BY ROUTINE)
;       R1=USED AS A FLAG
;       R3=LOW WORD RESULT
;       R4=MULTIPLICAN & HIGH WORD RESULT
        R0=%0
        R1=%1
        R2=%2
        R3=%3
        R4=%4
        R5=%5
        R6=%6
        R7=%7
        SP=R6
        PC=R7
        .TITLE  MULPLY
        .GLOBL  MULPLY
        .CSECT
MULPLY: CLR     R1             ;CLEAR FLAG REGISTER
        CLR     R3             ;CLEAR LOW ORDR RESULT
        TST     R0             ;NEGATIVE MULTIPLIER?
        BEQ     MZERO          ;MAKE ZERO FAST
        BGT     MULT01         ;POSITIVE,GO AROUND
        INC     R1             ;SET NEGATIVE FLAG
        NEG     R0             ;MAKE POSITIVE
MULT01: TST     R4             ;TEST MULTIPLICAND
        BEQ     MZERO          ;IF ZERO GO AWAY
        BGT     MULT02         ;IF + JUMP AROUND
        INC     R1             ;INC NEGATIVE FLAG
        NEG     R4             ;MAKE POSITIVE
MULT02: ASL     R4             ;       "
MULT03: ASL     R4             ;       "
        BCC     MULT04         ;SKIP IF NO CARRY
        ADD     R0,R3          ;ADD IN MULTIPLICAND
MULT04: ASL     R3             ;SHIFT MAGNITUTDE AGIN
        ROL     R4             ;       "
        BCC     MULT05         ;SKIP AGAIN IF NO CARRY
        ADD     R0,R3          ;ADD IN MULTIPLICAND
        ADC     R4             ;ADD LOW ORDER OVERFLOW
MULT05: ASL     R3             ;DO IT AGAIN
        ROL     R4  
        BCC     MULT06
        ADD     R0,R3
        ADC     R4
MULT06: ASL     R3             ;DO IT AGAIN!
        ROL     R4
        BCC     MULT07
        ADD     R0,R3
        ADC     R4
MULT07: ASL     R3             ;DO IT AGAIN!!
        ROL     R4
        BCC     MULT08
        ADD     R0,R3
        ADC     R4
MULT08: ASL     R3             ;DO IT AGAIN!!!
        ROL     R4
        BCC     MULT09
        ADD     R0,R3
        ADC     R4
MULT09: ASL     R3             ;DO IT DO IT DO IT!
	ROL     R4
        BCC     MULT10
        ADD     R0,R3
        ADC     R4
MULT10: ASL     R3             ;DO IT,DO IT, DO WHOW!
        ROL     R4
        BCC     MULT11
        ADD     R0,R3
        ADC     R4
MULT11: ASL     R3             ;--- HO BOY! ---
        ROL     R4
        BCC     MULT12
        ADD     R0,R3
        ADC     R4
MULT12: ASL     R3             ;THIS IS TOO MUCH!
        ROL     R4
        BCC     MULT13
        ADD     R0,R3
        ADC     R4
MULT13: ASL     R3             ;GETTING BORING?
        ROL     R4
        BCC     MULT14
        ADD     R0,R3
        ADC     R4
MULT14: ASL     R3             ;LA LA DA DA DA LA LA DAA
        ROL     R4
        BCC     MULT15
	ADD     R0,R3
        ADC     R4
MULT15: ASL     R3             ;FAST IS WEIRD!!
        ROL     R4
        BCC     MULT16
	ADD     R0,R3
        ADC     R4
MULT16: ASL     R3             ;COME ON , COME ON!
        ROL     R4
        BCC     MULT17
	ADD     R0,R3
        ADC     R4
MULT17: ASL     R3             ;I CANT BELIEVE IT
        ROL     R4
        BCC     MULT18
	ADD     R0,R3
        ADC     R4             ;--- FINALLY DONE SHIFT-ADDING!!
MULT18: BIT     #1,R1          ;LOOK AT SIGN BIT
	BEQ     MPOS           ;RESULT IS + JUMP AROUND
        COM     R3             ;DOUBLE PRECISION 
        COM     R4             ;NEGATION OF
        CLC                    ;CLEAR FOR TWO'S COMPLMNT
        INC     R3             ;PRODUCT
        ADC     R4             ;   "
        BR      MPOS           ;SKIP ZERO ROUTINE
MZERO:  CLR     R3             ;MULTIPLY BY ZERO
        CLR     R4
MPOS:   RTS     PC             ;LEAVE THIS PLACE
        .END
      
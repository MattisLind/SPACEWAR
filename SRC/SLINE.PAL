;*****************************************************
;
;               SHORT LINE
;               (SLINE)
;
;               VERSION         1D
;
;       BILL SEILER AUG 17,1974
;
;       DRAWS SHORT LINES MADE OF 16 POINTS
;       R2=XINITIAL
;       R3=YINITIAL
;       R4=XFILNAL
;       R5=YFINAL
;       R0=COUNTER
;       R1=SCRATCH REG.
        R0=%0
        R1=%1
        R2=%2
        R3=%3
        R4=%4
        R5=%5
        R6=%6
        R7=%7
        SP=R6
        PC=R7
        .TITLE  SLINE
        .GLOBL  SLINE,DAC0,DAC1,DPCS,ERASE
        .CSECT
SLINE:  SUB     R2,R4           ;GET DELTA X
        SUB     R3,R5           ;GET DELTA Y
        ASR     R4
        ASR     R4
        ASR     R4
        ASR     R4              ;XDELTA/16
        ASR     R5
        ASR     R5
        ASR     R5
        ASR     R5              ;YDELTA/16
        BIS     #ERASE,DPCS     ;TURN DOT OFF
        MOV     #16.,R0         ;CTR = 16
SL01:   MOV     R2,R1           ;R1=XINITIAL
        ASR     R1
        ASR     R1
        ASR     R1
        ASR     R1              ;THROW OUT 4 BITS
        ASR     R1
        ASR     R1              ;THROW TWO MORE BITS
        MOV     R1,DAC0         ;PUT XINITIAL IN DAC0
        MOV     R3,R1           ;R1=YINITIAL
        ASR     R1
        ASR     R1
        ASR     R1
        ASR     R1              ;DUMP 4 BITS
        ASR     R1
        ASR     R1              ;AND YET TWO BITS
        MOV     R1,DAC1         ;PUT YINITIAL IN DAC1
        BIC     #ERASE,DPCS     ;TURN DOT ON;
        ADD     R4,R2           ;INCREMENT XINITIAL
        ADD     R5,R3           ;INCREMENT YINITIAL
        BIS     #ERASE,DPCS     ;TURN DOT OFF
        DEC     R0              ;IS CTR ZERO YET?
        BGE     SL01            ;NO-THEN DO AGAIN
        RTS     PC              ;YES-ALL DONE!
        .END

      

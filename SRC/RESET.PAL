;********************************************************
;
;               SUBROUTINE RESET
;
;               VERSION         4B
        R0=%0
        R1=%1
        R2=%2
        R3=%3
        R4=%4
        R5=%5
        R6=%6
        R7=%7
        SP=R6
        PC=R7
;       RESETS RKTS FOR RESTART OF PROGRAM
;
        ORBIT=400
        CENTER=16384.
        .TITLE  RESET
        .GLOBL  RESET,YSIZE1,ORBIT,CENTER,RES01
        .CSECT
RESET:  MOV     #YSIZE1,R2       ;SET UP PTR
;**************RESET AND REPOSITION ROCKET ONE**************
        CLR     (R2)+           ;CLR YSIZE1
        CLR     (R2)+           ;CLR FLAG1
        CLR     (R2)+           ;CLR XVELL1
        MOV     RES05,(R2)+     ;ORBIT VELOCITY IN YVELH1
        MOV     RES01,(R2)+     ;ORBIT POSITION IN YDISP1
        CLR     (R2)+           ;CLR XSIZE1
        CLR     (R2)+           ;CLR XVELL1
        MOV     RES06,(R2)+     ;ORBIT VELOCITY IN XVELH1
        MOV     RES02,(R2)+     ;ORBIT POSITION IN XDISP
        CLR     (R2)+           ;CLR ANGL1
        CLR     (R2)+           ;CLR ANGH1
;**************RESET AND REPOSITION ROCKET TWO*************
        CLR     (R2)+           ;CLR YSIZE2
        CLR     (R2)+           ;CLR FLAG2
        CLR     (R2)+           ;CLR XVELL2
        MOV     RES07,(R2)+     ;ORBIT VELOCITY IN YVELH2
        MOV     RES03,(R2)+     ;ORBIT POSITION IN YDISP2
        CLR     (R2)+           ;CLR XSIZE2
        CLR     (R2)+           ;CLR XVELL2
        MOV     RES08,(R2)+     ;ORBIT VELOCITY IN XVELH2
        MOV     RES04,(R2)+     ;ORBIT POSITION IN XDISP2
        CLR     (R2)+           ;CLR ANGL2
        CLR     (R2)            ;CLR ANGH2
;**************ROTATE POSITION AND VELOCITY DATA***********
        MOV     RES01,-(SP)     ;SAVE FIRST NUMBER
        MOV     RES02,RES01
        MOV     RES03,RES02
        MOV     RES04,RES03     ;ROTATE UP WORDS
        MOV     (SP)+,RES04     ;PUT SAVED VALUE IN LAST WORD
        MOV     RES08,-(SP)     ;SAVE FIRST VELOCITY
        MOV     RES07,RES08
        MOV     RES06,RES07
        MOV     RES05,RES06     ;ROTATE DOWN 4 WORDS
        MOV     (SP)+,RES05     ;PUT SAVE VALUE IN FIRST WORD
        RTS     PC              ;ALL DONE---------------!!!
RES01:  .WORD   CENTER
RES02:  .WORD   CENTER
RES03:  .WORD   -CENTER
RES04:  .WORD   -CENTER
RES05:  .WORD   0
RES06:  .WORD   0
RES07:  .WORD   0
RES08:  .WORD   0
        .END


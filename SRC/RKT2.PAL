;******************************************************
;
;               SUBROUTINE RKT2
;               VERSION         1C
;               BILL SEILER
;               AUG. 24,1974
;       DRAWS ROCKET # TWO
;       USES ALL REGISTERS AND THE STK
        R0=%0
        R1=%1
        R2=%2
        R3=%3
        R4=%4
        R5=%5
        R6=%6
        R7=%7
        SP=R6
        PC=R7
        .TITLE  RKT2
        .GLOBL  SLINE,RKT2,XDISP2,YDISP2,XSIZE2,YSIZE2
        .CSECT
RKT2:   MOV     XDISP2,R2       ;R2=XD2
        MOV     YDISP2,R3       ;R3=YD2
        MOV     XSIZE2,R0       ;R0=XS2
        MOV     YSIZE2,R1       ;R1=YS2
        MOV     R2,R4           ;R4=XD2
        MOV     R3,R5           ;R5=YD2
;*********POINT A********
        ADD     R0,R4           ;R4=XD2+XS2
        ADD     R1,R5           ;R5=YD2+YS2
;*********POINT B********
        SUB     R0,R2           ;R2=XD2+XS2
        SUB     R1,R3           ;R3=YD2-YS2
        ASR     R0              ;R0=XS2/2
        ASR     R1              ;R1=YS2/2
;*********POINT E********
        MOV     R5,-(SP)        ;YD2+YS2
        MOV     R4,-(SP)        ;XD2+XS2
;*********POINT D********
        MOV     R3,-(SP)        ;YD2-YS2
        ADD     R0,(SP)         ;YS2+XS/2-YS2
        MOV     R2,-(SP)        ;XD2-XS2
        SUB     R1,(SP)         ;XD2-YS/2-XS2
;*********POINT C********
        MOV     R3,-(SP)        ;YD2-YS2
        ADD     R1,(SP)         ;YD2-YS2/2
        MOV     R2,-(SP)        ;XD2-XS2
        ADD     R0,(SP)         ;XD2-XS2/2
;*********POINT F*******
        MOV     R3,-(SP)        ;YD2-YS2
        SUB     R0,(SP)         ;YD2-XS2/2-YS2
        MOV     R2,-(SP)        ;XD2-XS2
        ADD     R1,(SP)         ;XD2+YS/2-XS2
;*********DRAW LINES*********
        JSR     PC,SLINE        ;DRAW LINE # 1
        MOV     (SP)+,R4
        MOV     (SP)+,R5        ;GET POINT F
        JSR     PC,SLINE        ;DRAW LINE # 2
        MOV     (SP)+,R4
        MOV     (SP)+,R5        ;GET POINT C
        JSR     PC,SLINE        ;DRAW LINE # 3
        MOV     (SP)+,R4
        MOV     (SP)+,R5        ;GET POINT D
        JSR     PC,SLINE        ;DRAW LINE # 4
        MOV     (SP)+,R4
        MOV     (SP)+,R5        ;GET POINT E
        JSR     PC,SLINE        ;DRAW LINE # 5
        RTS     PC              ;ALL DONE!!!!!!!!!!!!!!!!!!!
        .END
